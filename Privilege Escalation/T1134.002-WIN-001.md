# T1134.002 - Access Token Manipulation: Create Process with Token

## Hunt Tags

**ID:** T1134.002

**Last Modified:** 5/19/2020

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post]()

## ATT&CK Tags

**Tactic:** Privilege Escalation,Defense Evasion

**Technique:** Access Token Manipulation: Create Process with Token (T1134.002)
Attackers utilize the runas command to create a new process to escalate privileges and bypass access controls

## Technical description of the attack

## Permission required to execute the technique

User

## Detection description

This rule detects the use of the runas command and checks whether the account used to elevate privileges isn't the users own admin account. Additionally it wil match this event to the logon events to chech wether it has been successful as well as augment the event with the new SID.

## Utilized Data Source

| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceProcessEvents | MDE | Process monitoring |
| - | DeviceLogonEvents | MDE | Authentication loggingzot |

## Hunt details

### KQL

**FP Rate:** *Low*

**Source:** *MDE*

**Description:** *See above*

**Query:**

```C#
let RunAsProcess=DeviceProcessEvents
    | where FileName =~ "runas.exe" 
    // filter usage from the lapsadmin account, this can potentially be extended with certain admin accounts or prefixes
    // add these accounts to the array or add an 'or AccountName !startswith ""' in there 
    | where not(AccountName has_any("lapsadmin"))
    // de-obfuscate the commandline used 
    | extend CleanProcessCommandLine=parse_command_line(tostring(ProcessCommandLine), "windows")
    // exclude a user running something on their system through their admin account
    | where CleanProcessCommandLine !contains strcat("adm",AccountName) //replace this with your admin account naming convention
    // exclude local admin account activities by for instance the servicedesk, that uses the LAPS provisioned account
    | where not(CleanProcessCommandLine has_any (":lapsadmin","\\lapsadmin")) //replace this with your local admin account
    // extract the username for the elevation action
    | extend ElevatedAccountName=extract("user:([a-zA-Z0-9\\\\]+)",1,tostring(CleanProcessCommandLine))
    // strip the domain suffix
    | extend CleanElevatedAccountName= trim("(.*\\\\)",ElevatedAccountName);
RunAsProcess
| join kind=leftouter ( 
    DeviceLogonEvents
    | project-rename CleanElevatedAccountName = AccountName
    ) on CleanElevatedAccountName,DeviceId
| project-rename ElevatedActionType=ActionType1,ElevatedAccountSid=AccountSid1,IsLocalAdmin
| project Timestamp,DeviceId,DeviceName,FileName,FolderPath,ProcessCommandLine,SHA256,ProcessIntegrityLevel,AccountDomain,AccountName,AccountSid, LogonId, InitiatingProcessFileName,InitiatingProcessFolderPath,InitiatingProcessCommandLine,CleanProcessCommandLine,ElevatedAccountName,CleanElevatedAccountName,ElevatedActionType,LogonType,ElevatedAccountSid,IsLocalAdmin
```

### NOG DOEN

## Considerations

* Some additional filtering might be required bases on the setup of your environment
* Be sure to replace the admin prefix to your standard for admin accounts

## False Positives

* There might be legitimate processes tied to this activities in for instance kiosk environments, these can be whitelisted.
  
## Detection Blind Spots

* When this process is executed through the API or via a tool the attacker brought to tthe system this detection will not spot that behavior

## References

* https://attack.mitre.org/techniques/T1134/002/