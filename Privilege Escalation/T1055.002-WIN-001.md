# 0xFF-0086-Process_injection_via_NtAllocateVirtualMemoryRemote_by_rare_or_unsigned_processes-Win

## Metadata

**ID:** 0xFF-0086-Process_injection_via_NtAllocateVirtualMemoryRemote_by_rare_or_unsigned_processes-Win

**OS:** WindowsEndpoint, WindowsServer

**FP Rate:** Low

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post]()

---

## ATT&CK Tags

**Tactics:**
TA0004 - Privilege Escalation

| Technique | Subtechnique | Technique Name |
|---|---| --- |
| T1055 | 002 | Process Injection - Portable Executable Injection|

## Utilized Data Sources

| Log Provider | Event ID | Event Name | ATT&CK Data Source |
|---------|---------|----------|---------|
|MDE|DeviceEvents|||
---

## Technical description of the attack\
The query searches for unsigned processes with a low GlobalPrevalence that inject into other processes. This technique is commonly used by multiple attacker toolkits.


## Permission required to execute the technique
User

## Detection description
This rule detects the use of the NtAllocateVirtualMemoryRemote API call that is registered by MDE. The is a legitimate use for this, however when executed by a rare binary which is unsigned as well should be very uncommon.


## Considerations
Processes can be injected into in various different ways, this detection only focusses on the variant utilizing the NtAllocateVirtualMemoryRemote API call.


## False Positives
None expected.


## Suggested Response Actions
Validate the InitiatingProcessFolderPath and InitiatingProcessFileName and its commandline for known behavior. Investigate the host for other suspicious behavior. A common side signal for this would be a handle to the process with PROCESS_VM_WRITE permissions. Currently MDE only records this for the lsass.exe process.


## Detection Blind Spots
When the GlobalPrevalence of the used tool is too high this detection will not pick it up, the same is true for software signed with a validiated certificate. Additionally, when an attacker injects into a valid signed and common process through another method they will hide in the common signals.


## References

---

## Detection

**Language:** Kusto

**Platform:** M365 Security

**Query:**
```C#
let timeframe = (24h));
let remoteAlloc = DeviceEvents
| where Timestamp > ago(timeframe)
| where ActionType == "NtAllocateVirtualMemoryRemoteApiCall"
| where InitiatingProcessFileName !~ FileName
| where not(isempty(InitiatingProcessSHA1))
| summarize count(), make_set(InitiatingProcessFileName) ,make_set(FileName) by InitiatingProcessSHA1
| invoke FileProfile(InitiatingProcessSHA1, 1000) 
| where (GlobalPrevalence <= 200 or isempty(GlobalPrevalence)) and IsCertificateValid != 1;
DeviceEvents
| where Timestamp > ago(timeframe)
| where InitiatingProcessSHA1 in ((remoteAlloc | project InitiatingProcessSHA1)) and ActionType == "NtAllocateVirtualMemoryRemoteApiCall"
| join kind=leftouter remoteAlloc on InitiatingProcessSHA1
| summarize count() by FileName,InitiatingProcessFolderPath, InitiatingProcessSHA256,InitiatingProcessCommandLine, InitiatingProcessAccountName, InitiatingProcessVersionInfoProductName, InitiatingProcessVersionInfoOriginalFileName
// Customer specific tuning
```