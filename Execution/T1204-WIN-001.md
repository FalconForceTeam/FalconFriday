# T1204 - User-Execution
## Hunt Tags
**ID:** T1204

**Last Modified:** 9/10/2020

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post](TODO)

## ATT&CK Tags
**Tactic:** Execution

**Technique:** User-Execution (T1204)

## Technical description of the attack
Malicious (office) files is an technique where users are tricked to open a file controlled by an attacker, which in turn triggers code execution on the victims system.
A well known examples are various forms of office macros which in turn try to run other binaries (like lolbins) or inject code into other processes in order to gain code execution.
## Permission required to execute the technique
User
## Detection description
These rules attempt to detect 2 different, well documented methods to execute code from an office macro, including CactusTorch.
Rule 1 identifies cases where an office process starts another process and retrieves information about the started binary using the [FileProfile](https://docs.microsoft.com/en-us/microsoft-365/security/mtp/advanced-hunting-fileprofile-function?view=o365-worldwide) function. It then proceeds to check whether the started binary is: 
- a known lolbin, which is always flagged
OR
- not (signed MS binary)
AND
- not (signed other binary with a global prevalance > 100)
Rule 2 identifies cases where an office process injects code into another process through 'CreateRemoteThreadApiCall' or 'WriteProcessMemory'.
## Utilized Data Source
| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceImageLoadEvents | DATP | Process monitoring, Process Command-line parameters |
| - | DeviceProcessEvents | DATP | Process monitoring |
| - | DeviceEvents | DATP | Process monitoring | 
| - | FileProfile | DATP | -  |
## Hunt details
### KQL

**FP Rate:** *Medium*

**Source:** *DATP*

**Description:** *See above*

**Query:**

Rule 1 (started untrusted binaries)
```C#
let lolbins = dynamic(["At.exe", "Atbroker.exe", "Bash.exe", "Bitsadmin.exe", "CertReq.exe", "Certutil.exe", "Cmd.exe", "Cmdkey.exe", "Cmstp.exe", "Control.exe", "Csc.exe", "Cscript.exe", "Desktopimgdownldr.exe", "Dfsvc.exe", "Diantz.exe", "Diskshadow.exe", "Dnscmd.exe", "Esentutl.exe", "Eventvwr.exe", "Expand.exe", "Extexport.exe", "Extrac32.exe", "Findstr.exe", "Forfiles.exe", "Ftp.exe", "GfxDownloadWrapper.exe", "Gpscript.exe", "Hh.exe", "Ie4uinit.exe", "Ieexec.exe", "Ilasm.exe", "Infdefaultinstall.exe", "Installutil.exe", "Jsc.exe", "Makecab.exe", "Mavinject.exe", "Microsoft.Workflow.Compiler.exe", "Mmc.exe", "MpCmdRun.exe", "Msbuild.exe", "Msconfig.exe", "Msdt.exe", "Mshta.exe", "Msiexec.exe", "Netsh.exe", "Odbcconf.exe", "Pcalua.exe", "Pcwrun.exe", "Pktmon.exe", "Presentationhost.exe", "Print.exe", "Psr.exe", "Rasautou.exe", "Reg.exe", "Regasm.exe", "Regedit.exe", "Regini.exe", "Register-cimprovider.exe", "Regsvcs.exe", "Regsvr32.exe", "Replace.exe", "Rpcping.exe", "Rundll32.exe", "Runonce.exe", "Runscripthelper.exe", "Sc.exe", "Schtasks.exe", "Scriptrunner.exe", "SyncAppvPublishingServer.exe", "Ttdinject.exe", "Tttracer.exe", "vbc.exe", "Verclsid.exe", "Wab.exe", "Wmic.exe", "Wscript.exe", "Wsreset.exe", "Xwizard.exe", "AgentExecutor.exe", "Appvlp.exe", "Bginfo.exe", "Cdb.exe", "csi.exe", "Devtoolslauncher.exe", "dnx.exe", "Dotnet.exe", "Dxcap.exe", "Excel.exe", "Mftrace.exe", "Msdeploy.exe", "msxsl.exe", "ntdsutil.exe", "Powerpnt.exe", "rcsi.exe", "Sqldumper.exe", "Sqlps.exe", "SQLToolsPS.exe", "Squirrel.exe", "te.exe", "Tracker.exe", "Update.exe", "vsjitdebugger.exe", "Winword.exe", "Wsl.exe"]);
DeviceImageLoadEvents
| where FileName in~ ("mscoree.dll", "mscorlib.dll", "mscorlib.ni.dll") and InitiatingProcessFileName in~ ("winword.exe", "excel.exe", "powerpnt.exe")
    and InitiatingProcessCommandLine has_any (".doc", ".wbk", ".docm", ".dot", ".dotm" ".xls", ".xlsm", ".xltm", ".xla",".xll", ".xlam", ".ppt", ".pptm", ".pot", ".potm", ".ppsm", ".sldm") and not(InitiatingProcessCommandLine has_any (".docx", "dotx",".xlsx", ".xltx", ".pptx"))
| extend InitiatingProcessFileName=tolower(InitiatingProcessFileName)
| summarize by DeviceId, InitiatingProcessId, InitiatingProcessFileName
//the extra where inside DeviceProcessEvents is not necessary, but speeds up the join by A LOT!
| join kind=inner hint.strategy=broadcast (
    DeviceProcessEvents 
    | where InitiatingProcessFileName in~ ("winword.exe", "excel.exe", "powerpnt.exe") and FileName !in~ ("winword.exe", "excel.exe", "powerpnt.exe")
    | extend InitiatingProcessFileName=tolower(InitiatingProcessFileName)
) on DeviceId, InitiatingProcessFileName, InitiatingProcessId
| where not(isempty(FileName))
| invoke FileProfile("SHA1", 1000) 
| where FileName in~(lolbins) or (  //Always show lolbins
    not (IsCertificateValid and IsRootSignerMicrosoft) //Do not show signed MS binaries
    and not (GlobalPrevalence >= 500 and IsCertificateValid) //Do not show other signed binaries with a reasonable prevalence
    ) //otherwise show the binaries (everything that isn't signed, or has low rep)
```
Rule 2 (process injection)
```C#
DeviceImageLoadEvents
| where FileName in~ ("mscoree.dll", "mscorlib.dll", "mscorlib.ni.dll") and InitiatingProcessFileName in~ ("winword.exe", "excel.exe", "powerpnt.exe")
    and InitiatingProcessCommandLine has_any (".doc", ".wbk", ".docm", ".dot", ".dotm" ".xls", ".xlsm", ".xltm", ".xla",".xll", ".xlam", ".ppt", ".pptm", ".pot", ".potm", ".ppsm", ".sldm") and not(InitiatingProcessCommandLine has_any (".docx", "dotx",".xlsx", ".xltx", ".pptx"))
| extend InitiatingProcessFileName=tolower(InitiatingProcessFileName)
| summarize by DeviceId, InitiatingProcessId, InitiatingProcessFileName
| join kind=inner hint.strategy=broadcast (
    DeviceEvents 
    | where ActionType in ("CreateRemoteThreadApiCall", "WriteProcessMemory")
    | where InitiatingProcessFileName in~ ("winword.exe", "excel.exe", "powerpnt.exe")
    | extend InitiatingProcessFileName=tolower(InitiatingProcessFileName)
    ) 
on DeviceId, InitiatingProcessFileName, InitiatingProcessId
```

Rule 3 (cscript, wscript, mshta as host)
```C#
//.NET being loaded from wscript or cscript to run .net code such as cactustorch and sharpshooter
// all based on the DotNetToJScript by James Foreshaw documented here https://github.com/tyranid/DotNetToJScript
DeviceImageLoadEvents 
| where FileName in~ ("mscoree.dll", "mscorlib.dll", "mscorlib.ni.dll") 
| where tolower(InitiatingProcessFileName) in ("wscript.exe", "cscript.exe") or tolower(InitiatingProcessFileName) in ("mshta.exe")
```
## Considerations
* The GlobalPrevalence treshold require fine-tuning for your environment. Higher is better, but needs to be balanced with the false positive rate. Set the values as high as possible, where the false positive rate is still acceptable for you. 
* Should a valid signature be a reason to have a lower threshold for your environment?Â 
* The timeframe should be sufficiently short to ensure that the number of hashes is < 1000, because of the limitations in the FileProfile function.
## False Positives
* There are legitimate reasons an office application may call a lolbin. Rundll32.exe for printers and csc.exe for defender tasks come to mind. 
* Some management & installation scripts use vbs/jscript files for legit purposes. 
## Detection Blind Spots
* Process injection using techniques not based on CreateRemoteThread are not detected.
* Use of new / private / undocumented LOLBINs are not detected.
* "Bring-your-own-land" and hide from the LOLBIN filter by dropping your own LOLBIN using a different filename.
* Place your JScript/HTA/VBS in a file/folder which is commonly whitelisted by blue teams to reduce noise.
* Social engineer your victim to open your maldoc via "File => Open" instead of double clicking it from Explorer/Outlook.
## References
* https://github.com/mdsecactivebreach/CACTUSTORCH
* https://attack.mitre.org/techniques/T1204/002/
