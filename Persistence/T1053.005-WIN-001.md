# T1053.005 - Scheduled Task/Job: Scheduled Task

## Hunt Tags

**ID:** T1053.005-WIN-001.md

**Last Modified:** 22/01/2021 16:00

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post](https://medium.com/falconforce/malicious-scheduled-tasks-debc64633f81)

## ATT&CK Tags

**Tactic:** Execution, Persistence, Privilege Escalation

**Technique:** Scheduled Task/Job: Scheduled Task
​

## Technical description of the attack
​
In order to gain persistence an attacker might create a scheduled task that launches a malicious binary in the future.

## Permission required to execute the technique

Administrator

## Detection description
Scheduled tasks run with svchost parent process and parent process command line arguments "-k netsvcs -p -s Schedule". This detection rule obtains unique hashes of binaries that are running with this parent process. To identify potential malicious binaries only the unsigned binaries with a low global prevalence are retained.

## Utilized Data Source
| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceProcessEvents | DATP | Process command-line parameters, Process monitoring, Windows event logs |

​
## Hunt details

### KQL
**FP Rate:** *Medium*

**Source:** *DATP*

**Description:** *Scheduled tasks run with svchost parent process and parent process command line arguments "-k netsvcs -p -s Schedule". This detection rule obtains unique hashes of binaries that are running with this parent process. To identify potential malicious binaries only the unsigned binaries with a low global prevalence are retained.*

**Query:**

```C#
let scheduled_binaries = DeviceProcessEvents
| where InitiatingProcessCommandLine == "svchost.exe -k netsvcs -p -s Schedule"
| distinct SHA1;
let untrusted_binaries = scheduled_binaries
| join kind=leftanti (DeviceFileCertificateInfo | summarize max_trusted=max(IsTrusted) by SHA1 | where max_trusted==1) on SHA1;
untrusted_binaries
| invoke FileProfile(SHA1,1000)
| where IsCertificateValid != 1 // Exclude signed binaries
| where GlobalPrevalence < 1000
| join (DeviceProcessEvents | where InitiatingProcessCommandLine == "svchost.exe -k netsvcs -p -s Schedule") on SHA1
| summarize arg_max(Timestamp, *) by SHA1 // Give last execution with all details per SHA1
```

## Considerations

* Not everything unsigned is bad, but probably worth being aware of and checking out.

## False Positives

There are legitimate binaries that are unsigned and uncommon such as in-house developed binaries or binaries related to niche commercial products that are not wide spread.

## Detection Blind Spots

* An attacker could use a signed binary such as cmd.exe or a LOLBIN to run malicious code indirectly.

## References

