Note: You are viewing an old, archived version of this content. The latest version is available in the ['main' branch](https://github.com/FalconForceTeam/FalconFriday/blob/main/0xFF-0069-Suspicious_office_child_process_created-Win.md).

# T1566.002 - Phishing: Spearphishing link
## Hunt Tags
**ID:** T1566.002

**Last Modified:** 18/12/2020

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post](https://medium.com/falconforce/falconfriday-catching-more-macros-0xff0a-ec8273ab115a?source=friends_link&sk=90696a8b7003c5c71f5b2c91575281c0)

## ATT&CK Tags
**Tactic:** Initial Access

**Technique:** Phishing: Spearphishing Link

## Technical description of the attack
The attacker sends a spearphishing email to a user. The email contains a link, which points to a website that eventually presents the user a download of an MS Office document. This document contains a malicious macro and provides initial access to the user's machine. 

## Permission required to execute the technique
User

## Detection description
The query looks for file downloads from the internet based on network connections. The query tries to match FileCreate events with network activity based on time proximity. Next, the query allows to filter out downloads from whitelisted domains. Finally, it shows all office process that have opened the downloaded file **and** have spawned a child process. 

## Utilized Data Source
| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceProcessEvents | MDE | Process monitoring |
| - | DeviceFileEvents | MDE | Process monitoring |
| - | DeviceNetworkEvents | MDE | Process monitoring |


## Hunt details
### KQL

**FP Rate:** *Medium*

**Source:** *MDE*

**Description:** *See above*

**Query:**

```C#
let browsers = dynamic(["iexplore.exe", "chrome.exe", "firefox.exe", "msedge.exe"]);
let ext = dynamic([".docm", ".xlsm", ".xls", ".doc", ".pptm", ".ppt"]);
let officeApps = dynamic(["winword.exe", "excel.exe", "powerpnt.exe"]);
let whitelist = dynamic(["MSOSYNC.exe", "splwow64.exe", "csc.exe", "outlook.exe", "AcroRd32.exe", "Acrobat.exe", "explorer.exe", "DW20.exe", "Microsoft.Mashup.Container.Loader.exe", "Microsoft.Mashup.Container.NetFX40.exe", "WerFault.exe", "CLVIEW.exe"]);
let whitelistedDomains = dynamic(["yourtennant.sharepoint.com"]); 
let binPeriodForSearch = 15m;
let timeDiffFileCreateNetworkEvent = 15; //in seconds, don't make this 15s because of comparison later on. 
//List all filecreate events where the filename has a known office extension which can contain macros
let fileDownloads = materialize(DeviceFileEvents
//we need to have FileCreated and FileRenamed here because some browsers first download the file under a different name and rename it when it's done.
//e.g. the chrome .crdownload files are all renamed to the intended name after the download has finished. 
| where ActionType in ("FileCreated", "FileRenamed") and InitiatingProcessFileName in~ (browsers) and FileName has_any (ext)
//we need to do this, to limit the search of deviceNetworkEvents. Otherwise, the dataset becomes too big to join in MDE.
| extend period=bin(Timestamp, binPeriodForSearch)
//optimizations to keep MDE happy. Otherwise the databasets get too big
| project DeviceId, InitiatingProcessFileName, InitiatingProcessId, period, FileName, Timestamp
//the renames are meant to avoid confusions as there will be a lot of FileNames from different tables.
| project-rename DeviceFileEvents_InitiatingProcessFileName = InitiatingProcessFileName, 
                 DeviceFileEvents_FileName = FileName,
                 DeviceFileEvents_Timestamp = Timestamp);
//now we need to find the networkevent that triggered the the filewrite.
//this is an approximation based on timestamp, deviceid, pid and processname
let downloadSource = materialize(DeviceNetworkEvents
| where DeviceId in ((fileDownloads | project DeviceId)) and RemotePort in (80, 443)
| extend period=bin(Timestamp, binPeriodForSearch)
//exclude whitelisted domains. Here you want to whitelist your internal sharepoint environment
//this can be useful if you want to use this rule for external attacks.
//there is a trade-off as an internal attacker might abuse your sharepoint for malware.
| where parse_url(RemoteUrl).Host !in~ (whitelistedDomains)
| lookup kind=inner fileDownloads on DeviceId, InitiatingProcessId, $left.InitiatingProcessFileName == $right.DeviceFileEvents_InitiatingProcessFileName, period
| extend TimeDiff = datetime_diff('second', Timestamp, DeviceFileEvents_Timestamp)
//the filecreate and network event should happen within max 15 second of eachother. 
| where  -timeDiffFileCreateNetworkEvent < TimeDiff and TimeDiff < timeDiffFileCreateNetworkEvent
// we're now only interessted in the unique filenames of the downloads and the location they're possibly downloaded from
| summarize possibelURLs=make_set(RemoteUrl) by DeviceFileEvents_FileName
);
//final step in tying everything together. 
//find office applications that create a child process which is in our previously generated list
//we don't fileter on devicename because we want to see all instances of this file being run.
DeviceProcessEvents
| where InitiatingProcessFileName in~ (officeApps) and FileName !in~ (officeApps) and FileName !in~ (whitelist) and FileName !in~ (browsers)
| project-rename DeviceProcessEvents_InitiatingProcessCommandLine = InitiatingProcessCommandLine
| where DeviceProcessEvents_InitiatingProcessCommandLine has_any (( downloadSource | project DeviceFileEvents_FileName))
```

## Considerations
* The current whitelist is quick and dirty. Use full pathnames instead of filenames for whitelisting child processes.
* You can improve this query by further filtering the results for office processes that have loaded the VBE7.dll, required for macro execution
* If you're concerned about macro executions directly from email, you can add outlook.exe as "grand parent" instead of only browsers. 
* The current whitelist of remote hosts only performs full host name matching. You can extend this to perform subdomain matching using regexes or IP-range matching using ipv4_is_match()/ipv6_is_match. 
* Be careful not to whitelist LOLBINs. 

## False Positives
*  
  

## Detection Blind Spots
* Currently, the whitelist based on filenames allows an easy bypass. 

## References
*  