# T1110.003 - Brute Force: Password Spraying
## Hunt Tags
**ID:** T1110.003

**Last Modified:** 23/04/2021

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post](https://medium.com/falconforce/falconfriday-password-spraying-with-out-mdi-0xff10-c9cc260ac04a?source=friends_link&sk=d56fba60633b0d8158d8b81e71a6c8e5)

## ATT&CK Tags
**Tactic:** Initial Access

**Technique:** Brute Force: Password Spraying

## Technical description of the attack
The query searches for failed logins from a single source towards multiple different accounts and provides three parameters to tune
the results for your specific environment. The full details of the working are described in the blog post linked above.
 

## Permission required to execute the technique
User

## Detection description
A password spraying attack is detected, where a single machine has performed a large number of failed login attempts, with a large number of different accounts. For each account, the attacker uses just a few attempts to prevent account lockout. This query uses the Microsoft Defender for Identity (MDI) data as a datasource for the analysis. MDI has it's own password spraying detection, but that's not configurable. This implementation provides three variables to tune to your environment. 

## Utilized Data Source
| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | IdentityLogonEvents | MDI |  |


## Hunt details
### KQL

**FP Rate:** *Medium*

**Source:** *MDE*

**Description:** *See above*

**Query:**

```C#
let thresholdForUniqueFailedAccounts = 20;
let upperBoundOfFailedLogonsPerAccount = 10;
let ratioSuccessFailedLogons = 0.5;
let timeframe = 1d;
IdentityLogonEvents
| where Timestamp >= ago(timeframe)
| summarize SuccessLogonCount = countif(ActionType == "LogonSuccess"), FailedLogonCount = countif(ActionType == "LogonFailed"),
    UniqueAccountFailedLogons=dcountif(AccountUpn, ActionType == "LogonFailed"), FailedAccounts=make_set_if(AccountUpn, ActionType == "LogonFailed"),
    SuccessAccounts=make_set_if(AccountUpn, ActionType == "LogonSuccess"), FirstFailed=minif(Timestamp, ActionType == "LogonFailed"),
    LastFailed=maxif(Timestamp, ActionType == "LogonFailed"), LastTimestamp=arg_max(Timestamp, tostring(ReportId)) by IPAddress, DeviceName //IP address is here the "remote IP" , ie the source of the logon attempt
| where UniqueAccountFailedLogons > thresholdForUniqueFailedAccounts and SuccessLogonCount*ratioSuccessFailedLogons < FailedLogonCount and UniqueAccountFailedLogons*upperBoundOfFailedLogonsPerAccount > FailedLogonCount 
```

## Considerations
* This query needs tuning for your environment. A short guide is provided in the blog post linked above. 
* MDI has a built-in detection for password spraying, which works differently and cannot be tuned for more or less sensitivity. 

## False Positives
*  
  

## Detection Blind Spots
*  

## References
* https://github.com/Azure/Azure-Sentinel/blob/master/Detections/SigninLogs/SigninPasswordSpray.yaml
* https://docs.microsoft.com/en-us/defender-for-identity/compromised-credentials-alerts#suspected-brute-force-attack-kerberos-ntlm-external-id-2023




