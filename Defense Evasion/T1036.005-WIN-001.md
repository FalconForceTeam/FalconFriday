# T1036.005 - Masquerading: Match Legitimate Name or Location

## Hunt Tags

**ID:** T1036.005

**Last Modified:** 5/11/2020

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post]()

## ATT&CK Tags

**Tactic:** Defense Evasion

**Technique:** Masquerading: Match Legitimate Name or Location (T1036.005)
Attackers often match or approximate the name or location of legitimate files to avoid detection rules that are based trust of certain operating system processes.

## Technical description of the attack

## Permission required to execute the technique

User

## Detection description

This rule detects mismatches in the parent-child relationship of core operating system processes to uncover different masquerading attempts. 

## Utilized Data Source

| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceProcessEvents | MDE | Process monitoring |

## Hunt details

### KQL

**FP Rate:** *Low*

**Source:** *MDE*

**Description:** *See above*

**Query:**

```C#
let ProcessRelations=datatable(ImageFile:string,ExpectedParent:dynamic) [
    "smss.exe", dynamic(["smss.exe", "ntoskrnl.exe", ""]),
    "crmss.exe", dynamic(["smss.exe"]),
    "wininit.exe", dynamic(["smss.exe"]),
    "winlogon.exe", dynamic(["smss.exe"]),
    "services.exe", dynamic(["wininit.exe"]),
    "lsaiso.exe", dynamic(["wininit.exe"]),
    "lsass.exe", dynamic(["wininit.exe"]),
    "spoolsv.exe", dynamic(["services.exe"]),
    "dllhost.exe", dynamic(["svchost.exe"]),
    "lsm.exe", dynamic(["wininit.exe"]),
    "svchost.exe", dynamic(["services.exe", "msmpeng.exe"]),
    "runtimebroker.exe", dynamic(["svchost.exe"]),
    "taskhostw.exe", dynamic(["svchost.exe"]),
    "userinit.exe", dynamic(["winlogon.exe"])
    // Explorer can have a lot of parents in some environments
    //,"explorer.exe", dynamic(["userinit.exe"])
];
DeviceProcessEvents
| extend ImageFile = tostring(tolower(parse_path(tostring(FolderPath)).Filename))
| extend ParentFile = tostring(tolower(parse_path(tostring(InitiatingProcessFolderPath)).Filename))
| project Timestamp, ImageFile, ParentFile
| lookup kind=inner ProcessRelations on ImageFile
| where not(set_has_element(ExpectedParent,ParentFile))
| summarize count() by ImageFile, ParentFile
```

## Considerations

* The query is somewhat heavy on resources. Use it on a limited timefrime, for instance 24 hours - 7 days.
* Some of these parent processes launch their childs and immediately terminate, which in some cases might cause the parent field to remain empty.
* Some of the processes like dllhost can in some cases have other parent processes which can be legitimate, do validate them
* Be prepared to find malware and backdoors - don't run this query on Friday and ruin your weekend.

## False Positives

* For some reason, some software installers unpack and execute windows binaries using other names. This obviously triggers this rule.

## Detection Blind Spots

* There is an obvious blind spot for processes outside of the datatable.

## References

* https://www.andreafortuna.org/2017/06/15/standard-windows-processes-a-brief-reference/
* https://www.sans.org/security-resources/posters/dfir/hunt-evil-165