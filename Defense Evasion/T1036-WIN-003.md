# T1021.005 - Masquerading: Match Legitimate Name or Location
## Hunt Tags
**ID:** T1021.005

**Last Modified:** 06/11/2020

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post](TODO)

## ATT&CK Tags
**Tactic:** Defense Evasion

**Technique:** Masquerading: Match Legitimate Name or Location

## Technical description of the attack
This attack is fairly basic and aims to avoid detection of malicious applications. It does so by mimicking the name of legitimate binaries. 

## Permission required to execute the technique
User
## Detection description
This rules is aimed at detecting files which are normally supposed to be signed, but are executed unsigned.

Approach 1:List all (system32) binaries which are normally signed. Cross check against process executions which have the same filename, but are unsigned.

Approach 2: Make a list of the top 100 executed (system) processes. Obtain all unique hashes for those processes and filter all unique processes with a low prevalence and check if they're signed. This is based on the assumption that most commonly used files are usually signed. This assumptions doesn't always hold unfortunately, but the query does help in identifying suspicious unsigned files on your system. 

## Utilized Data Source
| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceProcessEvents | DATP | Process monitoring |
| - | FileProfile | DATP | Process use of network |

## Hunt details
### KQL

**FP Rate:** *Medium*

**Source:** *DATP*

**Description:** *See above*

**Query:**
Approach 1:
```C#
let signedSystemFiles = dynamic(["aitstatic.exe", "ApplicationFrameHost.exe", "ApplyTrustOffline.exe", "AppVClient.exe", "AppVDllSurrogate.exe", "AppVNice.exe", "AppVShNotify.exe", "audiodg.exe", "AuthHost.exe", "backgroundTaskHost.exe", "bcdedit.exe", "bdeunlock.exe", "BioIso.exe", "bootsect.exe", "browser_broker.exe", "CameraSettingsUIHost.exe", "CastSrv.exe", "CExecSvc.exe", "changepk.exe", "ClipRenew.exe", "ClipUp.exe", "CloudExperienceHostBroker.exe", "CloudNotifications.exe", "cmdiag.exe", "CompatTelRunner.exe", "consent.exe", "convertvhd.exe", "CredentialEnrollmentManager.exe", "CredentialUIBroker.exe", "csrss.exe", "DataExchangeHost.exe", "DeviceCensus.exe", "Dism.exe", "DisplaySwitch.exe", "dllhost.exe", "DTUHandler.exe", "easinvoker.exe", "ErgonomicKBNotificationService.exe", "fontdrvhost.exe", "FsIso.exe", "fsutil.exe", "GenValObj.exe", "hcsdiag.exe", "hvax64.exe", "hvc.exe", "hvix64.exe", "hvsievaluator.exe", "hvsimgr.exe", "hvsirdpclient.exe", "hvsirpcd.exe", "HvsiSettingsWorker.exe", "iotstartup.exe", "LicensingUI.exe", "LockAppHost.exe", "LockScreenContentServer.exe", "LsaIso.exe", "lsass.exe", "mavinject.exe", "mfpmp.exe", "MRT.exe", "MusNotifyIcon.exe", "NDKPing.exe", "NgcIso.exe", "nmbind.exe", "nmscrub.exe", "ntoskrnl.exe", "nvspinfo.exe", "OpenWith.exe", "PasswordOnWakeSettingFlyout.exe", "phoneactivate.exe", "PickerHost.exe", "PktMon.exe", "ProximityUxHost.exe", "prproc.exe", "ResetEngine.exe", "RuntimeBroker.exe", "ScriptRunner.exe", "securekernel.exe", "SecurityHealthHost.exe", "SecurityHealthService.exe", "services.exe", "sessionmsg.exe", "SettingSyncHost.exe", "SgrmBroker.exe", "SgrmLpac.exe", "SIHClient.exe", "SlideToShutDown.exe", "smss.exe", "SndVol.exe", "spaceman.exe", "sppsvc.exe", "svchost.exe", "SyncAppvPublishingServer.exe", "SysResetErr.exe", "systemreset.exe", "SystemSettingsAdminFlows.exe", "SystemSettingsBroker.exe", "SystemSettingsRemoveDevice.exe", "taskhostw.exe", "Taskmgr.exe", "tcblaunch.exe", "ttdinject.exe", "tttracer.exe", "ucsvc.exe", "upfc.exe", "UserAccountBroker.exe", "verifier.exe", "vmcompute.exe", "VmComputeAgent.exe", "vmms.exe", "vmplatformca.exe", "vmsp.exe", "vmwp.exe", "wcsetupagent.exe", "WerFault.exe", "WerFaultSecure.exe", "wermgr.exe", "wifitask.exe", "wimserv.exe", "wininit.exe", "winload.exe", "winresume.exe", "wkspbroker.exe", "wlrmdr.exe", "WpcMon.exe", "wuauclt.exe", "WUDFCompanionHost.exe", "WWAHost.exe", "AdtAgent.exe", "appverif.exe", "iaStorAfsNative.exe", "iaStorAfsService.exe", "MCU.exe", "microsoft.windows.softwarelogo.showdesktop.exe", "MpSigStub.exe", "RtkAudUService64.exe", "TsWpfWrp.exe"]);
let uniqueHashes = materialize(
    DeviceProcessEvents 
    | where FileName in~ (signedSystemFiles) and not(isempty(SHA1))
    | summarize count() by SHA1
);
let unsignedHashes = materialize(
    uniqueHashes
    //take 1000 of the most unique hashes as files with high prevelance are very likely to be legitly signed
    | top 1000 by count_ asc 
    | invoke FileProfile(SHA1, 1000)
    | where IsCertificateValid != 1 or (IsRootSignerMicrosoft != 1 and GlobalPrevalence < 200)
);
DeviceProcessEvents
| where SHA1 in ((unsignedHashes | project SHA1)) //this where is for performance improvement.
| join kind=inner unsignedHashes on SHA1
```


Approach 2:
```C#
let onlyForSystemFiles = false;
let allProcNames = materialize(
    DeviceProcessEvents
    | where iff(onlyForSystemFiles, (FolderPath startswith "C:\\Windows\\system32\\" or FolderPath startswith "C:\\windows\\syswow64"), true)
    | where FileName !in~ ("robocopy.exe") //add your favorites here
    | where FileName !contains "setup.exe" //installer are very noisy, we're getting rid of them. Tune to your flavor.
    | extend FileName = tolower(FileName)
    | summarize count()  by FileName
    | top 100 by count_ desc
);
let uniqueHashes = materialize(
    DeviceProcessEvents 
    | where FileName in~ ((allProcNames|project FileName)) and not(isempty(SHA1))
    | summarize count() by SHA1
);
let unsignedHashes = materialize(
    uniqueHashes
    //take 1000 of the most unique hashes as files with high prevelance are very likely to be legitly signed
    | top 1000 by count_ asc 
    | invoke FileProfile(SHA1, 1000)
    | where IsCertificateValid != 1 or (IsRootSignerMicrosoft != 1 and GlobalPrevalence < 200)
);
DeviceProcessEvents
| where SHA1 in ((unsignedHashes | project SHA1))
| join kind=inner unsignedHashes on SHA1
```

## Considerations
* For approach 1, there are a lot more binaries which can be added to the list. The current list is based on the system32 folder of my lab machine. 
* 

## False Positives
*  robocopy.exe
*  
  

## Detection Blind Spots
* The current detection rule doesn't "pin" the certificates. So if the certificate is publicly verifiable, it assumes the file is safe. You could improve this by pinning to the expected issuer or root CA. 


## References
*  https://www.ired.team/offensive-security/lateral-movement/lateral-movement-abusing-service-configuration-manager