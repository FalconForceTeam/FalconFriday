# T1036.001 - Masquerading: Invalid Code Signature - Network Connections

## Hunt Tags

**ID:** T1036.001-WIN-001

**Last Modified:** 27/08/2020 10:00

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post](https://medium.com/falconforce/falcon-friday-detecting-malicious-browser-extensions-and-code-signing-0xff01-db622e6a6519?sk=aad0b191d66145e16824058aba788b8c)

## ATT&CK Tags

**Tactic:** Defense Evasion

**Technique:** Masquerading: Invalid Code Signature (T1036.001)
​

## Technical description of the attack
​
In order to evade detection an attacker may use binaries that mimic a trusted certificate or modified signed binaries to make outbound network connections for various reasons. The binary can also be completely unsigned which stands out even more.

## Permission required to execute the technique

User

## Detection description
By combining the network connection events towards non-private IP addresses with the certificate validation information of the binaries in question we can have a fairly trustworthy means of detection.

## Utilized Data Source
| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceNetworkEvents | DATP | Process use of Network |
| - | DeviceFileCertificateInfo | DATP | Binary File Metadata |
​
## Hunt details

### KQL
**FP Rate:** *Medium*

**Source:** *DATP*

**Description:** *This detection looks at network connections towards non-RFC-1918 IP addresses. Next it queries all the signature of all binaries that are either unsigned of where the signature is untrusted. These two sets are joined and the result is connections by binaries with unsigned or untrusted certificates*

**Query:**

```C#
// set timeframe, default is 1day to allow for large environments, in smaller sets (<5K endpoints) you could extend this
let TimeFrame = ago(1d);
// get processes calling outbound to the internet
let networkConnections = 
    DeviceNetworkEvents
    // filter all RCF-1918 address space
    | where not(ipv4_is_match(RemoteIP, '192.168.0.0/16') or ipv4_is_match(RemoteIP, '172.16.0.0/12') or ipv4_is_match(RemoteIP, '10.0.0.0/8')) and Timestamp > TimeFrame
    // skipping system processes
    and InitiatingProcessId !in (0, 4)
    // only established connections
    and ActionType  == "ConnectionSuccess"
    // rename hash field to allow for later joining
    | project-rename SHA1 = InitiatingProcessSHA1
    | project Timestamp,DeviceName,DeviceId,InitiatingProcessFileName,InitiatingProcessFolderPath, InitiatingProcessCommandLine ,InitiatingProcessId, RemoteIP, RemoteUrl, SHA1;
// get binaries that are not signed or binaries of which the certificateState is untrusted
let certificateState = 
    DeviceFileCertificateInfo   
    | where Timestamp >= TimeFrame
    | where IsSigned == 0 or IsTrusted == 0
    | project DeviceId, SHA1;
// join the connections to the internet and let the ones remain with questionable certificate certificateState
let unsignedConnections = 
	networkConnections
	| join kind=leftsemi (certificateState) on DeviceId,SHA1;
// query results
unsignedConnections
```

## Considerations

* This search is a bit resource intensive, on larger data volumes Defender might complain, restrict the scope there
* Not everything unsigned is bad, but probably worth being aware of and checking out.
* Untrusted is probable bad, since the cert is revoked.

## False Positives

There are legitimate binaries that are unsigned, even Microsoft supplied ones like powershell.exe. However you might want to be interested in connections towards the internet from those as well.

## Detection Blind Spots

* There are ways to sign software with a faked certificate, so signed and trusted doesn't mean it is completely safe.

## References

* Any reference links that were helpful in your research
