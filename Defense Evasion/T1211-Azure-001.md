Note: You are viewing an old, archived version of this content. The latest version is available in the ['main' branch](https://github.com/FalconForceTeam/FalconFriday/blob/main/0xFF-0148-ASR_Bypass_Executable_Content-Win.md).

# ASR Bypassing Writing Executable Content

## Metadata

**ID:** ASR_Bypassing_Writing_Executable_Content

**OS:** Windows

**FP Rate:** Medium

---

## ATT&CK Tags

**Tactics:**
TA005 - Defense Evasion

| Technique | Subtechnique | Technique Name |
|---|---| --- |
| T1211 |  | Exploitation for Defense Evasion|
​
## Utilized Data Sources

| Log Provider | Event ID | Event Name | ATT&CK Data Source |
|---------|---------|----------|---------|
|MDE|DeviceFileEvents|||
---

## Technical description of the attack
An attacker can bypass the ASR rule `Block Office applications from creating executable content` by first writing the
file with an extension that is considered non-executable (e.g. .txt) and then renaming it to an executable extension (e.g. .ps1). 


## Permission required to execute the technique
User

## Detection description
The query checks for any file which has been created/written by an Office application and shortly after renamed to one of the 
blacklisted "executable extensions" which are text files. (e.g. ps1, .js, .vbs). 


## Considerations
The list of executable extensions that are text files is not complete. You can extend it with additional extension if desired. 


## False Positives
The rule triggers some false positives when a .lnk file is created automatically sometimes. We don't know exactly when this happens,
however, the .lnk can't be excluded as dropping a .lnk in the Windows startup folder is exactly the type of attack you want to prevent. 


## Suggested Response Actions
Obtain a copy of the file created and analyse the file for malicious content/behaviour. 


## Detection Blind Spots
There is another way to bypass this rule which is not publicly documented. 


## References
 - https://blog.sevagas.com/IMG/pdf/bypass_windows_defender_attack_surface_reduction.pdf
---
​

## Detection

**Language:** Kusto

**Platform:** Sentinel

**Query:**
```C#
let timeframe = 1d;
let executableExtensions = dynamic([".js", ".hta", ".lnk", ".application", ".vb", ".vba", ".vbs", ".ps", ".ps1", ".bat", ".cmd"]);
DeviceFileEvents
| where Timestamp >= ago(timeframe)
| where InitiatingProcessFileName in~ ("winword.exe", "excel.exe", "outlook.exe", "powerpnt.exe")
| where ActionType == "FileRenamed"
// the mv-apply is less performance compared to doing a full written out !endswith, but 
// we don't hit any limits and this is more readable/maintainable code. 
| mv-apply ext=executableExtensions to typeof(string) on 
(
    where PreviousFileName !endswith ext and FileName endswith ext
)
| project-reorder PreviousFileName, FileName
// Begin whitelist
// End whitelist
```

