# T1036.003 - Masquerading: Rename System Utilities
## Hunt Tags
**ID:** T1036.003

**Last Modified:** 9/10/2020

**Author:** [FalconForce](https://falconforce.nl/)

**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post](TODO)

## ATT&CK Tags
**Tactic:** Defense Evasion

**Technique:** Masquerading: Rename System Utilities (T1036.003)
Attackers often use LOLBINs that are renamed to avoid detection rules that are based on filenames. 

## Technical description of the attack


## Permission required to execute the technique
User
## Detection description
This rule detects renamed LOLBINs by first searching for all the known SHA1 hashes of the LOLBINs in your DeviceProcessEvents. This list is then used as reference to find other files executed which have a name that doesn't match the original file name. 
## Utilized Data Source
| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceProcessEvents | DATP | Process monitoring |
| - | - | LOLBAS Project | - |

## Hunt details
### KQL

**FP Rate:** *Medium*

**Source:** *DATP*

**Description:** *See above*

**Query:**

Rule 1 (Search for a specific executable)
```C#
//This is an efficient query for a specific lolbin.
let lolbin = "cmd.exe"; 
let allHashesLolbin = DeviceProcessEvents
| where FileName =~ lolbin and not(isempty(SHA1))
| summarize by SHA1;
DeviceProcessEvents
| where SHA1 in (allHashesLolbin) and FileName !~ lolbin
```
Rule 2 (Search all lolbins from the lolbas project)
```C#
// This is the generalized version which is way more intensive due to the join on 2 large sets.
// This query used "18% of your organization's allocated resources for the current 15 minutes".
// For a 7day lookback. 
// Also, removed update.exe as LOLBIN. It's a way too generic name and causes lot of noise. 
let lolbin = dynamic(["At.exe", "Atbroker.exe", "Bash.exe", "Bitsadmin.exe", "CertReq.exe", "Certutil.exe", "Cmd.exe", "Cmdkey.exe", "Cmstp.exe", "Control.exe", "Csc.exe", "Cscript.exe", "Desktopimgdownldr.exe", "Dfsvc.exe", "Diantz.exe", "Diskshadow.exe", "Dnscmd.exe", "Esentutl.exe", "Eventvwr.exe", "Expand.exe", "Extexport.exe", "Extrac32.exe", "Findstr.exe", "Forfiles.exe", "Ftp.exe", "GfxDownloadWrapper.exe", "Gpscript.exe", "Hh.exe", "Ie4uinit.exe", "Ieexec.exe", "Ilasm.exe", "Infdefaultinstall.exe", "Installutil.exe", "Jsc.exe", "Makecab.exe", "Mavinject.exe", "Microsoft.Workflow.Compiler.exe", "Mmc.exe", "MpCmdRun.exe", "Msbuild.exe", "Msconfig.exe", "Msdt.exe", "Mshta.exe", "Msiexec.exe", "Netsh.exe", "Odbcconf.exe", "Pcalua.exe", "Pcwrun.exe", "Pktmon.exe", "Presentationhost.exe", "Print.exe", "Psr.exe", "Rasautou.exe", "Reg.exe", "Regasm.exe", "Regedit.exe", "Regini.exe", "Register-cimprovider.exe", "Regsvcs.exe", "Regsvr32.exe", "Replace.exe", "Rpcping.exe", "Rundll32.exe", "Runonce.exe", "Runscripthelper.exe", "Sc.exe", "Schtasks.exe", "Scriptrunner.exe", "SyncAppvPublishingServer.exe", "Ttdinject.exe", "Tttracer.exe", "vbc.exe", "Verclsid.exe", "Wab.exe", "Wmic.exe", "Wscript.exe", "Wsreset.exe", "Xwizard.exe", "AgentExecutor.exe", "Appvlp.exe", "Bginfo.exe", "Cdb.exe", "csi.exe", "Devtoolslauncher.exe", "dnx.exe", "Dotnet.exe", "Dxcap.exe", "Excel.exe", "Mftrace.exe", "Msdeploy.exe", "msxsl.exe", "ntdsutil.exe", "Powerpnt.exe", "rcsi.exe", "Sqldumper.exe", "Sqlps.exe", "SQLToolsPS.exe", "Squirrel.exe", "te.exe", "Tracker.exe", "vsjitdebugger.exe", "Winword.exe", "Wsl.exe"]); 
let allHashesLolbin = materialize(
DeviceProcessEvents
| where FileName in~ (lolbin) and not(isempty(SHA1))
| extend OriginalFilename = tolower(FileName)
| summarize by SHA1, OriginalFilename);
allHashesLolbin
| join kind=inner hint.strategy = broadcast DeviceProcessEvents on SHA1
| where FileName !~ OriginalFilename
| where not(OriginalFilename =~ "bash.exe" and FileName =~ "sh.exe")
| extend message=strcat("Usage of ", OriginalFilename, ". Renamed as ", FileName)
| project-reorder message
```

## Considerations
*  The second query is really heavy on resources. Use it with care :) If you have performance optimizaion, would really appreciate a PR or a message on Twitter. 
*  Be prepared to find malware and backdoors - don't run this query on Friday and ruin your weekend. 

## False Positives
*  For some reason, some software installers unpack and execute windows binaries using other names. This obviously triggers this rule. 

## Detection Blind Spots
*  There is a large blindspot in the query as is, as it can only recognize versions of LOLBINs that have been used under their real name in the last 30 days in your organization. It would be better to use an external datasource (from Winbindex) if you want better coverage. 

## References
*  https://github.com/m417z/winbindex
*  https://lolbas-project.github.io/
*  








