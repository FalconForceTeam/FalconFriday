# LSASS Dumping using Debug Privileges

## Metadata
**ID:** LSASS_Dumping_using_Debug_Privileges-Win

**OS:** WindowsEndpoint, WindowsServer

**FP Rate:** Medium

---

## ATT&CK Tags

| Tactic | Technique | Subtechnique | Technique Name |
|---|---|---| --- |
| TA0006 - Credential Access | T1003 | 001 | OS Credential Dumping - LSASS Memory|
| TA0002 - Execution | T1106 |  | Native API|

## Utilized Data Sources

| Log Provider | Event ID | Event Name | ATT&CK Data Source | ATT&CK Data Component|
|---------|---------|----------|---------|---------|
|MDE|OpenProcessApiCall|DeviceEvents|Process|Process Access|
---

## Technical description of the attack
â€‹This query searches for a process that requests the `SeDebugPrivilege` privilege and opens LSASS memory using specific permission 0x1fffff which represents `PROCESS_ALL_ACCESS`.


## Permission required to execute the technique
Administrator

## Detection description
Attackers can extract credentials from LSASS memory by performing a memory dump of the LSASS process. Many methods of dumping LSASS memory require the `SeDebugPrivilege` privilege and use the `WriteMiniDump` function which opens the targeted process using `PROCESS_ALL_ACCESS` permissions.


## Considerations
None.


## False Positives
There are some applications that perform these actions for legitimate purposes, one example is Procmon by Sysinternals.


## Suggested Response Actions
Investigate what triggered the LSASS memory access. Investigate the system for other signs of compromise.


## Detection Blind Spots
Some tools can be modified to change the indicators of compromise by changing the permissions using which the LSASS process is opened.


## References
* https://github.com/bleedx-93/NihilistGuy
* https://downloads.volatilityfoundation.org//omfw/2012/OMFW2012_Gurkok.pdf

---

## Detection

**Language:** Kusto

**Platform:** M365 Security

**Query:**
```C#
let timeframe = 2*1h;
(if the entity is a mailbox)
let SeDebugPrivilege = binary_shift_left(1, 20); // Value for SeDebugPrivilege is 2**20 = 0x100000.
let LSASSOpen=materialize (
    DeviceEvents
    | where ingestion_time() >= ago(timeframe)
    | where ActionType == "OpenProcessApiCall"
    | where FileName =~ "lsass.exe"
    | extend AccessRights=parse_json(AdditionalFields).DesiredAccess
    | where AccessRights == 0x1fffff // PROCESS_ALL_ACCESS
    | summarize by DeviceId, InitiatingProcessId, InitiatingProcessSHA1
);
DeviceEvents
| where ingestion_time() >= ago(timeframe)
| where ActionType == "ProcessPrimaryTokenModified"
// Look for processes that request debug privilege that also opened LSASS
| where InitiatingProcessSHA1 in ((LSASSOpen | project InitiatingProcessSHA1)) // speeds up the query
| join kind=inner LSASSOpen on DeviceId, InitiatingProcessSHA1, InitiatingProcessId
// Check that debug privilege is enabled
| extend AdditionalFields=parse_json(AdditionalFields)
| extend CurrentTokenPrivEnabled = toint(AdditionalFields.CurrentTokenPrivEnabled)
| extend OriginalTokenPrivEnabled = toint(AdditionalFields.OriginalTokenPrivEnabled)
// Value for SeDebugPrivilege is 2**20 = 0x100000.
// Refer to https://downloads.volatilityfoundation.org//omfw/2012/OMFW2012_Gurkok.pdf for numeric values for privileges.
| extend DebugPrivCurrent = binary_and(CurrentTokenPrivEnabled,SeDebugPrivilege) == SeDebugPrivilege
| extend DebugPrivOrig = binary_and(OriginalTokenPrivEnabled,SeDebugPrivilege) == SeDebugPrivilege
// Check for processes that have debug privilege after the event but did not have it before.
| where not(DebugPrivOrig) and DebugPrivCurrent
| project-reorder Timestamp, DeviceId, InitiatingProcessFileName
```