# T1543.003 - Create or Modify System Process: Windows Service

## Hunt Tags

**ID:** T1543.003-WIN-001

**Last Modified:** 03/12/2020

**Author:** [FalconForce](https://falconforce.nl/)
​
**License:** [BSD 3-Clause License](https://github.com/FalconForceTeam/FalconFriday/blob/master/LICENSE)

**References:** [Link to medium post](https://medium.com/falconforce/falconfriday-rpc-service-creation-sharprdp-0xff08-8bf15744ca04)

## ATT&CK Tags

**Tactic:** Persistence

**Technique:** Create or Modify System Process: Windows Service (T1543.003)
​

## Technical description of the attack

Windows services can be used as a means of persistence in an environment, and can be created in multiple ways, one of which is remotely. Doing so often involves this service creation will be executed over RPC.

## Permission required to execute the technique

(Local) administrator

## Detection description

The provided detection is looking at established incoming connections towards the services process. This event is then joined by the creation of a registry key that is loaded on boot. To make it clear, changing the registry key does not load the service immediately, it will be on the next boot when it takes effect.

## Utilized Data Source

| Event ID | Event Name | Log Provider | ATT&CK Data Source |
|---------|---------|----------|---------|
| - | DeviceRegistryEvents | MDE | Windows Registry |
| - | DeviceNetworkEvents | MDE | Network Monitoring |

## Hunt details

### KQL

**FP Rate:** *High*

**Source:** *MDE*

**Description:**

**Query:**

```C#
let netevents=DeviceNetworkEvents 
| where ActionType == "InboundConnectionAccepted"
| where InitiatingProcessFolderPath == @"c:\windows\system32\services.exe"
// IMPORTANT There is some legitimate use for maintenance by support teams, filter their IP addresses/blocks below
| where not(RemoteIP has_any ("maintenance-ip-1","maintenance-ip-2","maintenance-ip-3"))
| project Timestamp,DeviceId,ActionType,InitiatingProcessFolderPath, DeviceName, RemoteIP, InitiatingProcessId;
let regevents=DeviceRegistryEvents 
| where RegistryKey contains @"\System\CurrentControlSet\Services" or RegistryKey contains @"\System\ControlSet001\Services"
| where ActionType contains "Created"
|project DeviceId, ActionType, RegistryKey, RegistryValueType,RegistryValueData, InitiatingProcessFolderPath,InitiatingProcessId, DeviceName;
let rpcservices = 
	netevents
	| join kind=leftouter  (regevents) on DeviceId, InitiatingProcessFolderPath,InitiatingProcessId;
rpcservices
| project Timestamp,DeviceName,RemoteIP,ActionType1 ,RegistryKey, RegistryValueType, RegistryValueData
|summarize count() by RemoteIP
```

## Considerations

- Depending on how your IT organisation is set up it might require filtering the IP addresses of the maintenance servers/jump hosts, a simple *"|summarize count() by RemoteIP"* will point help you find them quite quickly.

## False Positives

- Maintenance systems as described above

## Detection Blind Spots


## References
